#!/usr/bin/env bash
#23980376
#Sai Charan Pokuri

# Validate the input file
if [ "$#" -ne 1 ]; then
    exit 1
fi

datafile="$1"

if [ ! -f "$datafile" ]; then
    exit 1
fi

# Output file name with .tsv extension
output_file="${datafile%.*}.tsv"

# AWK preprocessing and output to .tsv file
awk -F';' '
BEGIN {
    OFS = "\t"
    max_seen_id = 0
}
# Process header row (first row)
NR == 1 {
    for (h = 1; h <= NF; h++) {
        gsub(/\r/, "", $h)
        printf "%s", $h
        if (h < NF) printf OFS
        else printf "\n"
    }
    next
}

{
    for (col = 1; col <= NF; col++) {
        gsub(/\r/, "", $col)
	# Convert decimal commas to dots for numeric fields (col 9 and 11)
        if (col == 9 || col == 11) {
            gsub(/,/, ".", $col)
        }
	 # Remove non-ASCII characters
        gsub(/[^\t -~]/, "", $col)
    }
    # If ID field (column 1) is blank, assign a new unique ID
    if ($1 ~ /^[[:space:]]*$/) {
        if (max_seen_id == 0) {
            while ((getline l < ARGV[1]) > 0) {
                split(l, fields, ";")
                if (fields[1] ~ /^[0-9]+$/ && fields[1] > max_seen_id)
                    max_seen_id = fields[1]
            }
            close(ARGV[1])
        }
        max_seen_id++
        $1 = max_seen_id
    }
    # Output the cleaned and processed row with tab-separated values
    for (col = 1; col <= NF; col++) {
        printf "%s", $col
        if (col < NF) printf OFS
        else printf "\n"
    }
}
' "$datafile" > "$output_file"
